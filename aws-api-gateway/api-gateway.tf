resource "aws_api_gateway_rest_api" "api_gw" {
  name                      = "${var.api_gw_name}"
  description               = "${var.api_description}"
  endpoint_configuration {
    types = ["REGIONAL"]
  }
}

resource "aws_api_gateway_vpc_link" "vpc_link" {
  name                      = "${var.vpc_link_name}"
  description               = "VPC link to spinnaker gate NLB generated by terraform armory scripts"
  target_arns               = ["${var.nlb_arn}"]
}

resource "aws_api_gateway_model" "api_gw_model" {
  rest_api_id = "${aws_api_gateway_rest_api.api_gw.id}"
  name = "${var.api_gw_model_name}"
  description = "JSON model to send parameters to spinnaker generated by terraform armory scripts"
  content_type = "application/json"
  schema = "${var.api_gw_model}"
}

resource "aws_api_gateway_request_validator" "validator" {
  name                        = "spin_validator"
  rest_api_id                 = "${aws_api_gateway_rest_api.api_gw.id}"
  validate_request_body       = "${var.api_validate_body}"
  validate_request_parameters = "${var.api_validate_parameters}"
}

resource "aws_api_gateway_resource" "api_gw_resource" {
  rest_api_id               = "${aws_api_gateway_rest_api.api_gw.id}"
  parent_id                 = "${aws_api_gateway_rest_api.api_gw.root_resource_id}"
  path_part                 = "${var.api_path_part}"
}

resource "aws_api_gateway_method" "api_gw_method_post" {
  rest_api_id               = "${aws_api_gateway_rest_api.api_gw.id}"
  resource_id               = "${aws_api_gateway_resource.api_gw_resource.id}"
  http_method               = "POST"
  authorization             = "NONE"
  api_key_required          = true
  request_validator_id      = "${aws_api_gateway_request_validator.validator.id}"
  request_models = {
    "application/json" = "${aws_api_gateway_model.api_gw_model.name}"
  }
  request_parameters = {
    "method.request.path.proxy" = false
  }
}

resource "aws_api_gateway_integration" "integration" {
  rest_api_id               = "${aws_api_gateway_rest_api.api_gw.id}"
  resource_id               = "${aws_api_gateway_resource.api_gw_resource.id}"
  http_method               = "${aws_api_gateway_method.api_gw_method_post.http_method}"
  type                      = "HTTP"
  integration_http_method   = "POST"
  passthrough_behavior      = "WHEN_NO_MATCH"
  content_handling          = "CONVERT_TO_TEXT"
  uri                       = "${var.uri_name}"
  connection_type           = "VPC_LINK"
  connection_id             = "${aws_api_gateway_vpc_link.vpc_link.id}"
}

resource "aws_api_gateway_method_response" "api_gw_method_response" {
  rest_api_id   = "${aws_api_gateway_rest_api.api_gw.id}"
  resource_id   = "${aws_api_gateway_resource.api_gw_resource.id}"
  http_method   = "${aws_api_gateway_method.api_gw_method_post.http_method}"
  status_code   = 200
  response_models = {
    "application/json" = "Empty"
  }
}

resource "aws_api_gateway_integration_response" "integration_response" {
  depends_on = ["aws_api_gateway_integration.integration"]

  rest_api_id   = "${aws_api_gateway_rest_api.api_gw.id}"
  resource_id   = "${aws_api_gateway_resource.api_gw_resource.id}"
  http_method   = "${aws_api_gateway_method.api_gw_method_post.http_method}"
  status_code = "${aws_api_gateway_method_response.api_gw_method_response.status_code}"
  
  response_templates {
    "application/json" = <<EOF

    EOF
  }
}

resource "aws_api_gateway_deployment" "api_deployment" {
  depends_on                = ["aws_api_gateway_integration.integration"]
  rest_api_id               = "${aws_api_gateway_rest_api.api_gw.id}"
  stage_name                = "${var.stage_name}"
}

resource "aws_api_gateway_usage_plan" "usage_plan" {
  name                      = "spin_api_usage"
  description  = "Usage plan associated to spinnaker api-key generated by terraform armory scripts"
  api_stages {
    api_id                  = "${aws_api_gateway_rest_api.api_gw.id}"
    stage                   = "${aws_api_gateway_deployment.api_deployment.stage_name}"
  }
}

resource "aws_api_gateway_api_key" "api_key" {
  name = "spin_api_key"
  description = "Api Key for Spinnaker webhooks generated by terraform armory scripts"
  enabled = true
}

resource "aws_api_gateway_usage_plan_key" "usage_plan_key" {
  key_id        = "${aws_api_gateway_api_key.api_key.id}"
  key_type      = "API_KEY"
  usage_plan_id = "${aws_api_gateway_usage_plan.usage_plan.id}"
}


